# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# PS4 Maintainer: joaoshiozo

pkgname=libdrm-ps4
_pkgname=libdrm
pkgver=2.4.115
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services for PS4 version"
url="https://dri.freedesktop.org/"
arch=(x86_64)
license=('custom')
depends=('libpciaccess')
makedepends=('python-docutils' 'meson')
checkdepends=('cairo')
provides=('libdrm')
conflicts=('libdrm')
replaces=('libdrm-new' 'libdrm-nouveau')
options=('debug')
source=(https://dri.freedesktop.org/$_pkgname/$_pkgname-$pkgver.tar.xz
        libdrm.patch
        COPYING)
sha512sums=('SKIP'
            'SKIP'
            'b0ca349b882a4326b19f81f22804fabdb6fb7aef31cdc7b16b0a7ae191bfbb50c7daddb2fc4e6c33f1136af06d060a273de36f6f3412ea326f16fa4309fda660')

prepare() {
    cd $_pkgname-$pkgver
    patch -Np1 < ../libdrm.patch
}

build() {
  arch-meson $_pkgname-$pkgver build \
    -D udev=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}
